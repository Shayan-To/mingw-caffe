# Maintainer: lemonsqueeze <lemonsqueeze@gmx.com>

_realname=caffe-cpu
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4078.7e970675c
pkgrel=1
pkgdesc='Deep learning framework (mingw-w64)'
arch=('any')
url=''
license=('custom')
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-tools-git"
  "${MINGW_PACKAGE_PREFIX}-gcc"
)
depends=(
  "${MINGW_PACKAGE_PREFIX}-boost"
  "${MINGW_PACKAGE_PREFIX}-protobuf-c"
  "${MINGW_PACKAGE_PREFIX}-gflags"
  "${MINGW_PACKAGE_PREFIX}-glog"
  "${MINGW_PACKAGE_PREFIX}-openblas")
source=("git+https://github.com/lemonsqueeze/caffe"
        "Makefile.config"
        "general.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd "${srcdir}/caffe"
  # Use master branch commit on which we're based,
  # not HEAD = current (mini branch) commit which is not terribly meaningful.
  printf "%s.%s.nohdf5.mini" "$(git rev-list --count master)" "$(git rev-parse --short master)"
}

prepare() {
  cd "${srcdir}/caffe"
  git checkout mini
  cp "../Makefile.config" .
  patch -p1 -i ../general.patch
}

build() {
  cd "${srcdir}/caffe"
  rm -rf ".build-${MINGW_CHOST}_release"

  make all distribute
}

package() {
  cd "${srcdir}/caffe"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"

  install -d "${pkgdir}${MINGW_PREFIX}/include"
  install -d "${pkgdir}${MINGW_PREFIX}/lib"
  install -d "${pkgdir}${MINGW_PREFIX}/bin"

  install -Dm644 "distribute-${MINGW_CHOST}/lib/libcaffe.a"      "${pkgdir}${MINGW_PREFIX}/lib/"

  cp -r "distribute-${MINGW_CHOST}/include/caffe"  "${pkgdir}${MINGW_PREFIX}/include/"
}
